plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.0'
}
version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

configurations {
    shade
    implementation.extendsFrom shade
}

loom {
    mixin {
        defaultRefmapName = "secretroutes.refmap.json"
    }
}

repositories {
    maven {url "https://maven.isxander.dev/releases"}

    // Added for LibAutoupdate
    maven { url "https://repo.nea.moe/releases" }

    maven { url 'https://repo.hypixel.net/repository/Hypixel/' }

    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"

    implementation 'net.hypixel:mod-api:1.0.1'

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    // LibAutoupdate
    modCompileOnly("moe.nea:libautoupdate:1.3.1") {
        transitive = true
    }
    shade("moe.nea:libautoupdate:1.3.1") {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

shadowJar {
    configurations = [project.configurations.shade]
}

remapJar {
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {}
}